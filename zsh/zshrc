#!/usr/bin/env zsh

bindkey -e


# PATH ========================================================================

# Git scripts (add custom git commands)
# path+=("$DOTFILES/git/git-scripts")


# Languages -------------------------------------------------------------------

[ -d "$(brew --prefix ruby)/bin" ] && path=("$(brew --prefix ruby)/bin" $path)

[ -f "$HOME/.ghcup/env" ] && source "$HOME/.ghcup/env"

[ -f "$HOME/.cargo/env" ] && source "$HOME/.cargo/env"

[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && \
  source "$HOME/.sdkman/bin/sdkman-init.sh"


# Mise-en-place ---------------------------------------------------------------

if command -v mise &>/dev/null; then
  eval "$(mise activate zsh)"     # Interactive Usage
  eval "$(mise hook-env -s zsh)"  # Usage in this script
fi


# General ---------------------------------------------------------------------

# User-specific scripts and executables
mkdir -p "$HOME/.local/bin"
path=("$HOME/.local/bin" $path)


# ZSH Plugin Manager (Zap) ====================================================

# Install
[ ! -d "${XDG_DATA_HOME:-$HOME/.local/share}/zap" ] && \
  zsh <(curl -s https://raw.githubusercontent.com/zap-zsh/zap/master/install.zsh) \
  --branch release-v1 --keep

# Init
[ -f "${XDG_DATA_HOME:-$HOME/.local/share}/zap/zap.zsh" ] && \
  source "${XDG_DATA_HOME:-$HOME/.local/share}/zap/zap.zsh"

# Plugins
plug "zsh-users/zsh-autosuggestions"
plug "zsh-users/zsh-completions"
plug "zsh-users/zsh-syntax-highlighting"
plug "hlissner/zsh-autopair"
plug "Aloxaf/fzf-tab"


# History =====================================================================

setopt hist_ignore_space    # Commands not saved if they are prepended with a space
setopt hist_ignore_all_dups # Remove all duplicate commands from history
setopt hist_save_no_dups    # Do not write duplicate commands to history file
setopt hist_ignore_dups     # Ignore duplicate commands in history
setopt hist_find_no_dups    # Do not display duplicate commands during history search


# Keybinds ====================================================================

bindkey '^p' history-search-backward
bindkey '^n' history-search-forward


# fzf Tab Completion ==========================================================

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' # case insensitive completion
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview \
  'eza --color=always --icons=always --group-directories-first --git $realpath'


# Starship Prompt =============================================================

if command -v starship &>/dev/null; then
  eval "$(starship init zsh)"
fi


# Gamon =======================================================================

if command -v gam &>/dev/null; then
  gam run             # Ensure correct GH CLI account on startup
  source <(gam hook)  # Hook into shell cd
fi


# Aliases =====================================================================

[ -f "$DOTFILES/zsh/aliases.sh" ] && source "$DOTFILES/zsh/aliases.sh"


# Functions ===================================================================

for file in $DOTFILES/zsh/functions/*(.); do
  source "$file"
done


# Shell Integrations ==========================================================

# enable fzf shell integration
source <(fzf --zsh)


# Completion ==================================================================

# Homebrew Completion
if type brew &>/dev/null; then
  fpath=("$(brew --prefix)/share/zsh/site-functions" $fpath)
fi

# User-specifc Completion
mkdir -p "$ZSH_COMPLETIONS"
fpath=("$ZSH_COMPLETIONS" $fpath)

if command -v terraform &>/dev/null; then
  complete -C /opt/homebrew/bin/terraform terraform
fi

# Load Shell Completion
autoload -Uz compinit
compinit

# this doesn't have any functionality yet
# alias f='find ~ ~/Developer/peter-bread ~/Developer/ak22112 ~/.config -mindepth 1 -maxdepth 1 -type d | sed "s|$HOME|~|g" | fzf'
